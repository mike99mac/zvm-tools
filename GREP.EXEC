/*-------------------------------------------------------------------*/
/* Name:  GREP EXEC - search files for text patterns                 */
/* Usage: grep pattern FN [FT [FM]]                                  */
/* License: MIT - https://en.wikipedia.org/wiki/MIT_License          */
/* Source: https://github.com/mike99mac/zvm-tools                    */
/*-------------------------------------------------------------------*/
Address COMMAND
Parse Arg args                       /* get all arguments            */
Parse Var args pattern args          /* pattern remains mixed case   */
Parse Upper var args fn ft fm .      /* fn, ft and fm are upper cased */
If (pattern = '' | pattern = '?') Then Do
  If (pattern = '') Then Say 'Error: Missing pattern'
  Say 'Name:  grep - search files for text patterns'
  Say 'Usage: grep pattern FN [FT [FM]]'
  Exit 1
End
'PIPE COMMAND LISTFILE' fn ft fm '| STEM files.'
If (files.0 = 0) Then Do
  Say 'No files found matching' fn ft fm
  Exit 2
End
Do i = 1 to files.0
  'PIPE |<' files.i '| locate /'||pattern||'/ | stem lines.'
  Do j = 1 to lines.0
    Say files.i||':' lines.j
  End
End
