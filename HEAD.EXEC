/*-------------------------------------------------------------------*/
/* HEAD EXEC - output the first part of files                        */
/* Syntax: HEAD [-n] fn ft [fm]                                      */
/* Source: https://github.com/mike99mac/zvm-tools                    */
/*-------------------------------------------------------------------*/
numLines = 10                        /* default number of lines */
parse upper arg args                 /* get args in upper case */
call parseArgs args                  /* parse arguments */
call fileHead                        /* produce output */
exit                                 /* all done */
 
/*-------------------------------------------------------------------*/
parseArgs: procedure expose fn ft fm numLines
/* Parse arguments                                                   */
/*-------------------------------------------------------------------*/
parse arg arg1 arg2 arg3 arg4
if arg1 = "?" | arg1 = "-H" | arg1 = "" then
  call help
if arg 2 = "" then do
  say "ERROR: not enough arguments"
  call help
end
if LEFT(arg1, 1) = "-" then do       /* -n flag supplied */
  numLines = SUBSTR(arg1, 2)         /* skip '-' */
  if DATATYPE(numLines, "number") = 0 then do
    say "ERROR:" numLines "is not a number"
    call help
  end
  if arg3 = "" then do
    say "ERROR: not enough arguments"
    call help
  end
  fn = arg2
  ft = arg3
  if arg4 = "" then
    fm = "A"
  else
    fm = arg4
end
else do                              /* num lines flag not supplied */
  fn = arg1
  ft = arg2
  if arg3 = "" then
    fm = "A"                         /* default file mode */
  else
    fm = arg3
end
return                               /* parseArgs() */
 
/*-------------------------------------------------------------------*/
fileHead: procedure expose fn ft fm numLines
/* Show the head of file(s)                                          */
/*-------------------------------------------------------------------*/
say "LISTFILE" fn ft fm
'PIPE COMMAND LISTFILE' fn ft fm '| STEM files.'
if files.0 = 0 then do               /* file not found */
  say "File not found"
  exit rc                            /* rc LISTFILE uses */
end
else if files.0 = 1 then             /* do not print file name */
  "TYPE" files.1 1 numLines          /* show the lines */
else do i = 1 to files.0             /* for all files */
  say "==>" files.i "<=="            /* show file name */
  "TYPE" files.i 1 numLines          /* show the lines */
  say                                /* leave some room */
end
exit                                 /* fileHead() */
 
/*-------------------------------------------------------------------*/
help:
/* Give help                                                         */
/*-------------------------------------------------------------------*/
say "Name:  HEAD EXEC - output the first part of files"
say "Usage: HEAD [-n] fn ft [fm]"
say "Where: 'fn ft' is the file name and type"
say "     : 'fm' is the file mode (default A)"
exit 2
 
